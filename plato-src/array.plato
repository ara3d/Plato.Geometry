
// Arrays provide a count and support the indexing operator [] 
interface IArray<T>
{
    Count(xs: Self): Integer;
    At(xs: Self, n: Integer): T;
}

// Extends the IArray interface with rows and columns. 
interface IArray2D<T>
    inherits IArray<T>
{ 
    RowCount(xs: Self): Integer;
    ColumnCount(xs: Self): Integer;
    At(xs: Self, column: Integer, row: Integer): T;
}

// Extends the IArray interface with rows, columns, and layers. 
interface IArray3D<T>
    inherits IArray<T>
{ 
    RowCount(xs: Self): Integer;
    ColumnCount(xs: Self): Integer;
    LayerCount(xs: Self): Integer;
    At(xs: Self, column: Integer, row: Integer, layer: Integer): T;
}

library ArrayLibrary
{
	Repeat(x: IAny, n: Integer): IArray<IAny>
        => n.MapRange((i) => x);

    Range(n: Integer): IArray<Integer>
        => n.MapRange((i) => i);

    Indices(xs: IArray<$T>): IArray<Integer>
		=> xs.Count.Range;

    Count(xs: IArray2D<$T>): Integer 
        => xs.RowCount * xs.ColumnCount;

    Count(xs: IArray3D<$T>): Integer 
        => xs.RowCount * xs.ColumnCount * xs.LayerCount;
    
    At(xs: IArray2D<$T>, i: Integer): $T 
        => xs.At(i % xs.ColumnCount, i / xs.ColumnCount);
    
    At(xs: IArray3D<$T>, i: Integer): $T 
        => xs.At(i % xs.ColumnCount, i / xs.ColumnCount, i / xs.LayerCount);    
        
    IsEmpty(xs: IArray<$T>): Boolean
        => xs.Count == 0;
    
    First(xs: IArray<$T>): $T 
        => xs[0];    
    
    Last(xs: IArray<$T>): $T
        => xs[xs.Count - 1];

    Middle(xs: IArray<$T>, n: Integer): $T
		=> xs[xs.Count / 2];

    Slice(xs: IArray<$T>, from: Integer, to: Integer): IArray<$T>
       => xs.Subarray(from, to - from);

    Slices(xs: IArray<$T>, size: Integer): IArray<IArray<$T>>
	    => (xs.Count / size).MapRange(i => xs.NthSlice(i, size));

    NthSlice(xs: IArray<$T>, n: Integer, size: Integer): IArray<$T>
        => xs.Subarray(n * size, size);

    Subarray(xs: IArray<$T>, from: Integer, count: Integer): IArray<$T>
        => count.MapRange((i) => xs.At(i + from));

    Skip(xs: IArray<$T>, n: Integer): IArray<$T>
		=> xs.Subarray(n, xs.Count - n);

    Take(xs: IArray<$T>, n: Integer): IArray<$T>
        => xs.Subarray(0, n);

    TakeLast(xs: IArray<$T>, n: Integer): IArray<$T>
        => xs.Skip(xs.Count - 1);

    Drop(xs: IArray<$T>, n: Integer): IArray<$T>
		=> xs.Take(xs.Count - n);

    Trim(xs: IArray<$T>, first: Integer, last: Integer): IArray<$T>
        => xs.Skip(first).Drop(last);

    Rest(xs: IArray<$T>): IArray<$T>
		=> xs.Skip(1);

    Reduce(xs: IArray<$T>, acc: $U, f: Function2<$T, $U, $U>): $U
    {
        var r = acc;
        for (var i=0; i < xs.Count; i = i + 1)
			r = f(xs.At(i), r);
        return r;
    }

    All(xs: IArray<$T>, f: Function1<$T, Boolean>): Boolean
    {
        for (var i=0; i < xs.Count; i = i + 1)
			if (!f(xs.At(i)))
				return false;
		return true;
	}

    Any(xs: IArray<$T>, f: Function1<$T, Boolean>): Boolean
    {
        for (var i=0; i < xs.Count; i = i + 1)
			if (f(xs.At(i)))
				return true;
		return false;
	}

    Map(xs: IArray<$T1>, f: Function1<$T1, $T2>): IArray<$T2> 
        => xs.Count.MapRange((i) => f(xs.At(i)));

    Zip(xs: IArray<$T1>, ys: IArray<$T2>, f: Function2<$T1, $T2, $T3>): IArray<$T3> 
        => xs.Count.Lesser(ys.Count).MapRange((i) => f(xs[i], ys[i]));  

    Zip(xs: IArray<$T1>, ys: IArray<$T2>, zs: IArray<$T3>, f: Function3<$T1, $T2, $T3, $T4>): IArray<$T4> 
        => xs.Count.Lesser(ys.Count).Lesser(zs.Count).MapRange((i) => f(xs[i], ys[i], zs[i]));  

    ModuloAt(xs: IArray<$T>, n: Integer): $T
		=> xs.At(n % xs.Count);

    Shift(xs: IArray<$T>, n: Integer): IArray<$T>
    	=> xs.Count.MapRange(i => xs.ModuloAt(i));

    WithNext(xs: IArray<$T0>, f: Function2<$T0, $T0, $T1>): IArray<$T1> 
        => xs.Drop(1).Zip(xs.Skip(1), f);

    WithNextAndBeginning(xs: IArray<$T0>, f: Function2<$T0, $T0, $T1>): IArray<$T1> 
        => xs.Zip(xs.Shift(1), f);

    WithNext(xs: IArray<$T0>, f: Function2<$T0, $T0, $T1>, connect: Boolean): IArray<$T1> 
        => connect ? xs.WithNextAndBeginning(f) : xs.WithNext(f);

    CartesianProduct(a: Integer, b: Integer, f: Function2<Integer, Integer, $T>): IArray2D<$T>
        => a.Range.CartesianProduct(b.Range, f);

    EveryNth(self: IArray<$T>, n: Integer): IArray<$T>
        => self.Indices().Map(i => self.ModuloAt(i * n));

    CartesianProduct(columns: IArray<$TColumn>, rows: IArray<$TRow>, func: Function2<$TColumn, $TRow, $TResult>): IArray2D<$TResult>
        => columns.Count.MakeArray2D(rows.Count, (i, j) => func(columns[i], rows[j]));

    Reverse(self: IArray<$T>): IArray<$T>
        => self.Indices().Map(i => self[self.Count - 1 - i]);

    Concat(xs: IArray<$T>, ys: IArray<$T>): IArray<$T>
        => (xs.Count + ys.Count).MapRange(i => i < xs.Count ? xs[i] : ys[i - xs.Count]);

    Prepend(self: IArray<$T>, value: $T): IArray<$T>
        => (self.Count + 1).MapRange(i => i == 0 ? value : self[i - 1]);

    Append(self: IArray<$T>, value: $T): IArray<$T>
        => (self.Count + 1).MapRange(i => i == 0 ? value : self[i - 1]);

    PrependAndAppend(self: IArray<$T>, before: $T, after: $T): IArray<$T>
        => self.Prepend(before).Append(after);

    //==
    // 2D functions 

    Map(xs: IArray2D<$T1>, f: Function1<$T1, $T2>): IArray2D<$T2> 
        => xs.ColumnCount.MakeArray2D(xs.RowCount, (a, b) => f(xs.At(a, b)));


} 