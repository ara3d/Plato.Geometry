// https://en.wikipedia.org/wiki/Top_type
// This is the Top Type in Plato. All types provide this. 
concept IAny
{ 
    FieldNames(x: Self): IArray<String>;     
    FieldValues(x: Self): IArray<Dynamic>;
    TypeName(x: Self): String;
}

// A value is a read-only type which has the implicit rule that only Values are members
concept IValue 
    inherits IAny, IEquatable
{ 
}

// A numerical type represents things like reals, integers, vectors, complex numbers, and measures.
// It is a generalization of real numbers to multiple dimensions, and with or without units of measure.
// The NumComponents function returns the dimensionality of the numerical type. 
// Individual components are returned via the Component type. 
concept INumerical 
    inherits IValue, IScalarArithmetic, IAdditive
{
    Components(x: Self): IArray<Number>;
    FromComponents(x: Self, xs: IArray<Number>): Self;
}   

// These are things that can be converted to and from numbers. It includes measures, and reals.
// Whole numbers are not included because converting to/from a number would change the result.
// It also includes Numbers of-course. 
concept INumberLike
    inherits INumerical, IOrderable
{
	ToNumber(x: Self): Number;
    FromNumber(x: Self, n: Number): Self;
}

// Represents a continuous one dimensional value.
// https://en.wikipedia.org/wiki/Real_number
// The "IValue" or a IReal is the number itself, and is equal to the Magnitude  
concept IReal
    inherits INumberLike, IAlgebraic, IArithmetic
{
}

// Used for integer and cardinal types. 
// Somewhat counter intuitively, they are not considered INumerical types.
// They do however support arithmetic operations.
concept IWholeNumber 
    inherits IValue, IOrderable, IArithmetic
{
}

// A measure is a numeric value with an intrinisic unit of measure. 
// It resembles a IReal except you cannot multiply or divide two measures without 
// getting a new type. It does however support IAdditive on itself.
// The idea of interpolation, combines a couple of existing concepts. 
concept IMeasure
    inherits INumberLike, IAdditive, IInterpolatable
{
}

// Vectors are specialization of the INumerical type.
// Unlike reals and measures they cannot be compared in any meaningful way 
// They are also "array-like" in that they have a fixed number of components.
concept IVector
    inherits INumerical, IArithmetic, IArray<Number>, IInterpolatable
{ 
}

// A coordinate is a point in some space (e.g., a cartesian grid). 
// This also includes a time line, so a DateTime is considered a ICoordinate. 
// Notice that coordinates do not provide arithmetic operators.  
// We could have defined a Point2D and a Point3D types as kinds of coordinates,  
// however, doing anything interesting with them involved converting them 
// to vectors. This just added a lot of complexity without much benefit. 
concept ICoordinate
    inherits IValue
{ }

// The less than function return true if the first value is strictly less than the other. 
concept IOrderable 
    inherits IEquatable
{
	LessThanOrEquals(x: Self, y: Self): Boolean;
}

// Supported for when two values are equal 
concept IEquatable
    inherits IAny
{
    Equals(a: Self, b: Self): Boolean;
}

// The formal term for this is an Abelian group over addition
concept IAdditive
    inherits IAny
{
    Add(a: Self, b: Self): Self;    
    Subtract(a: Self, b: Self): Self;    
    Negative(self: Self): Self;
}

// Types implementing this concept can be multiplied or divided by a scalar value
concept IScalarArithmetic
    inherits IAny
{ 
    Modulo(self: Self, other: Number): Self;    
    Divide(self: Self, other: Number): Self;    
    Multiply(self: Self, other: Number): Self;    
}

// A type which can be multiplied by itself
// Abelian semigroup over multiplication
concept IMultiplicative
    inherits IAny
{
    Multiply(a: Self, b: Self): Self;
}

// IMultiplicative inverse
concept IInvertible
    inherits IAny
{
    Inverse(self: Self): Self;  
}

// Represents an Abelian group under multiplication 
concept IMultiplicativeWithInverse
    inherits IMultiplicative, IInvertible
{
}

// A type which can be multiplied and added, and supports scalara arithmetic  
concept IAlgebraic
    inherits IInterpolatable, IMultiplicativeWithInverse, 
{   
}

// To be able to interpolate between two values requires addition, and scalar arithmetic
// This is a property shared by algebraic types and measurement types. 
concept IInterpolatable   
    inherits IAdditive, IScalarArithmetic
{ }

// A type which can be divided by itself 
concept IDivisible
    inherits IAny
{ 
    Divide(a: Self, b: Self): Self;    
}

// A type which supports modulo arithmetic
concept IModulo
    inherits IAny
{
    Modulo(a: Self, b: Self): Self;
}

// Basic arithmetic operators 
// Technically this is an arithmetic field + modulo operation
// https://en.wikipedia.org/wiki/Field_(mathematics)
concept IArithmetic 
    inherits IAdditive, IMultiplicative, IDivisible, IModulo
{ 
}

// Basic boolean operations, which are also similar to set operations.  
concept IBoolean
    inherits IAny
{
    And(a: Self, b: Self): Self;
    Or(a: Self, b: Self): Self;    
    Not(a: Self): Self;
}

// Represents a range of values. 
concept IInterval<T>    
    where T: INumerical
    inherits IEquatable, IValue, IArray<T>
{
    Min(x: Self): T;
    Max(x: Self): T;
}
